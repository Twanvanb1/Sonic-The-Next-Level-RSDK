// ----------------------------------
// RSDK Project:  Next Level RSDKv4
// Script Description:    GravSwitch Object 
// Script Author: Twanvanb1
// Generated by Twanvanb1's script generator
// ----------------------------------

// ========================
// Aliases
// ========================

private alias object.value0 : object.stood
private alias object.value1 : object.gravChange

private alias object.value31 : object.deformAmount

// Player Aliases
private alias object.ypos : player.ypos
private alias object.yvel : player.yvel
private alias object.gravity : player.gravity
private alias object.collisionBottom : player.collisionBottom

private alias object.value25 : player.gravityStrength
private alias object.value27 : player.jumpStrength

private alias 10 : SLOT_ZONESETUP

// ========================
// Function Declarations
// ========================

reserve function GravSwitch_DebugDraw
reserve function GravSwitch_DebugSpawn
reserve function GravSwitch_GravChange
reserve function GravSwitch_Deform

// ========================
// Static Values
// ========================

// ========================
// Tables
// ========================

// ========================
// Function Definitions
// ========================

private function GravSwitch_Deform
	//Reset array position:
	arrayPos0 = 0
	
	//Loop time!!
	while arrayPos0 < 0x200
	
		//Odd number of the loop
		temp0 = arrayPos0
		temp0 %= 2
		
		//Reset the scanline offset then move it to the right
		stage.deformationData0[arrayPos0] = 0	
		stage.deformationData0[arrayPos0] += object.deformAmount
		
		//If odd move the scanline to the left
		if temp0 == 1
			stage.deformationData0[arrayPos0] = 0
			stage.deformationData0[arrayPos0] -= object.deformAmount
		end if
		
		//Add for the loop!
		arrayPos0++
	loop
end function

private function GravSwitch_DebugDraw
	DrawSprite(0)
end function

private function GravSwitch_DebugSpawn
	CreateTempObject(TypeName[Gravity Switch], 0, object.xpos, object.ypos)
end function

private function GravSwitch_GravChange
	if player[currentPlayer].gravityStrength == 0x3800
		
		player[currentPlayer].gravityStrength = 0x2000
	else
		if player[currentPlayer].gravityStrength == 0x2000
			player[currentPlayer].gravityStrength = 0x3800
		end if
	end if
end function

private function GravSwitch_HandleDeform
	if object.deformAmount > -1
		tileLayer[0].deformationOffset++
		CallFunction(GravSwitch_Deform)
	end if
end function

// ========================
// Events
// ========================

//COMMENT YOUR SHIT THIS IS SO UGLY TO LOOK AT!!!

event ObjectUpdate
	object.stood = false
	foreach (GROUP_PLAYERS, currentPlayer, ACTIVE_ENTITIES)
		if object.frame == 0
			BoxCollisionTest(C_SOLID, object.entityPos, -14, -4, 14, 12, currentPlayer, C_BOX, C_BOX, C_BOX, C_BOX)
			if checkResult == COL_TOP
				object.stood = true
				
				//Play the sounds
				if player[currentPlayer].gravityStrength == 0x3800
					PlaySfx(SfxName[Gravity On], false)
				else
					PlaySfx(SfxName[Gravity Off], false)
				end if
				
				object.deformAmount = 24
				callFunction(GravSwitch_GravChange)
			end if
		else
			if player[currentPlayer].yvel >= 0
				BoxCollisionTest(C_PLATFORM, object.entityPos, -14, -4, 14, 12, currentPlayer, C_BOX, C_BOX, C_BOX, C_BOX)
				if checkResult == true
					object.stood = true
					player[currentPlayer].ypos += 0x20000
				else
					BoxCollisionTest(C_TOUCH, object.entityPos, -20, -12, 20, 8, currentPlayer, C_BOX, C_BOX, C_BOX, C_BOX)
					if checkResult == true
						player[currentPlayer].ypos = player[currentPlayer].collisionBottom
						FlipSign(player[currentPlayer].ypos)
						player[currentPlayer].ypos <<= 16
						player[currentPlayer].ypos += object.ypos
						player[currentPlayer].ypos -= 0x20000
						player[currentPlayer].gravity = GRAVITY_AIR
					end if
				end if
			end if
		end if
	next
	if object.deformAmount > 0
		object.deformAmount--
	end if
end event

event ObjectDraw
	callFunction(GravSwitch_HandleDeform)
	
	object.frame = object.stood
	DrawSprite(object.frame)
end event

event ObjectStartup
	LoadSpriteSheet("GPZ/Objects.gif")
	SpriteFrame(-16, -8, 32, 16, 99, 1)
	SpriteFrame(-16, -2, 32, 10, 99, 18)
	
	SetTableValue(TypeName[Gravity Switch], DebugMode_ObjCount, DebugMode_TypesTable)
	SetTableValue(GravSwitch_DebugDraw, DebugMode_ObjCount, DebugMode_DrawTable)
	SetTableValue(GravSwitch_DebugSpawn, DebugMode_ObjCount, DebugMode_SpawnTable)
	DebugMode_ObjCount++
	
end event

// ========================
// Editor Events
// ========================

event RSDKDraw
	DrawSprite(0)
end event

event RSDKLoad
	LoadSpriteSheet("GPZ/Objects.gif")
	SpriteFrame(-16, -8, 32, 16, 99, 1)
	
	SetVariableAlias(ALIAS_VAR_PROPVAL, "unused")
end event
